plugins {
    id "com.diffplug.spotless" version "6.25.0"
}

subprojects {
    apply plugin: "com.diffplug.spotless"
    apply plugin: 'java'

    spotless {
        java {
            importOrder()
            removeUnusedImports()
            googleJavaFormat()
            formatAnnotations()
            endWithNewline()
        }
    }

    group = 'org.tan'
    version = '0.16.0'

    compileJava.options.encoding = 'UTF-8'

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:deprecation"
    }

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name = "spigot-repo"
            url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }
        maven {
            name = "placeholderapi"
            url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
        maven {
            name = "placeholderapi"
            url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
        }
        maven {
            name = 'minecraft-repo'
            url = 'https://libraries.minecraft.net/'
        }
        maven {
            url = 'https://repo.dmulloy2.net/repository/public/'
        }
        maven {
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }
        maven {
            name = 'WorldGuard'
            url = "https://maven.enginehub.org/repo/"
        }

        maven { url = 'https://jitpack.io' } //Vault
        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url = 'https://oss.sonatype.org/content/repositories/central' }
        maven {
            url = 'https://repo.papermc.io/repository/maven-public/'
        }
        maven {
            name = 'folia'
            url = 'https://repo.papermc.io/repository/maven-public/'
        }

    }

    configurations.all {
        resolutionStrategy {
            force 'org.junit.platform:junit-platform-engine:1.10.0'
            force 'org.junit.platform:junit-platform-launcher:1.10.0'
            force 'org.junit.jupiter:junit-jupiter-api:5.10.0'
            force 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
        }
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }

    def targetJavaVersion = 21
    java {
        def javaVersion = JavaVersion.toVersion(targetJavaVersion)
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        if (JavaVersion.current() < javaVersion) {
            toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
            options.release = targetJavaVersion
        }
    }

    processResources {
        def props = [version: version]
        inputs.properties props
        filteringCharset = 'UTF-8'
        filesMatching('plugin.yml') {
            expand props
        }
    }
}

